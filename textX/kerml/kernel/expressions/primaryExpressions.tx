
/* Primary Expressions */

PrimaryExpression returns SysML::Expression :
	BaseExpression
	( {SysML::PathStepExpression.operand += current} '.'
	  operand += FeatureChainExpression
	)?
	( ( {SysML::OperatorExpression.operand += current}
	    operator = '[' operand += SequenceExpression ']'
	  | {SysML::OperatorExpression.operand += current} '->'
	    ownedRelationship += ReferenceTyping
	    ( ownedRelationship += ExpressionBodyMember
	    | ownedRelationship += FunctionReferenceMember
	    | ArgumentList
	    )
	  | {SysML::PathSelectExpression.operand += current} '.'
	    ownedRelationship += ExpressionBodyMember
	  )
	  ( {SysML::PathStepExpression.operand += current} '.'
	    operand += FeatureChainExpression
	  )?
	)*
;

ExpressionBodyMember returns SysML::FeatureMembership :
	ownedRelatedElement += ExpressionBody // ownedMemberFeature = BodyExpression
;

FunctionReferenceMember returns SysML::FeatureMembership :
	ownedRelatedElement += FunctionReference // ownedMemberFeature = FunctionReference
;

FunctionReference returns SysML::Expression :
	ownedRelationship += ReferenceTyping
;

FeatureChainExpression returns SysML::FeatureReferenceExpression :
	ownedRelationship += FeatureChainMember
;

FeatureChainMember returns SysML::Membership :
	  memberElement = [SysML::Feature | QualifiedName]
	| ownedRelatedElement += FeatureChain
;
